#version 460 core
#extension GL_NV_shader_atomic_float : enable

layout(local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

layout(std430, binding = 0) buffer PosBuffer {
    vec4 positions[];
};

layout(std430, binding = 1) buffer VBuffer {
    vec4 velocities[]; // vec3 velocity + float mass
};

layout(std430, binding = 2) buffer GridBuffer {
    vec4 gridStatus[]; // vec3 velocity + float mass
};

layout(std430, binding = 3) buffer CBuffer {
    mat3 affineC[]; // APIC affine velocity field
};

layout(std430, binding = 4) buffer FBuffer {
    mat3 F[]; // deformation gradient
};

uniform int gridRes;
uniform float gridSpacing;
uniform vec3 gridOrigin;
uniform float stiffness;            //3.
uniform float rest_density;         //4.
uniform float dynamic_viscosity;    //0.1
uniform float deltaTime; 

void main() {
    uint id = gl_GlobalInvocationID.x;

    float dx = 1.f / float(gridRes);
    float inv_dx = 1.f / dx;
    vec3 gridEnd = vec3(gridSpacing * gridRes) + gridOrigin;
    float mass = velocities[id].w;
    mat3 Cp = affineC[id];

    // grid space position
    vec3 pos = (positions[id].xyz - gridOrigin)/(gridEnd - gridOrigin) * gridRes;
    ivec3 base = ivec3(floor(pos - 0.5));
    vec3 fx = pos - base;

    // B-spline weights
    vec3 w[3];
    w[0] = 0.5 * (1.5 - fx) * (1.5 - fx);
    w[1] = 0.75 - (fx - 1.0) * (fx - 1.0);
    w[2] = 0.5 * (fx - 0.5) * (fx - 0.5);

    float density = 0.f;
    // loop neighbor nodes
    for (int i = 0; i < 3; ++i)
    for (int j = 0; j < 3; ++j)
    for (int k = 0; k < 3; ++k) {
        ivec3 node = base + ivec3(i, j, k);
        if (any(lessThan(node, ivec3(0))) || any(greaterThanEqual(node, ivec3(gridRes))))
            continue;

        int index = node.x + node.y * gridRes + node.z * gridRes * gridRes;

        float weight = w[i].x * w[j].y * w[k].z;
        density += weight * gridStatus[index].w;
    }

    // volume = mass/density
    float volume = velocities[id].w / density; 

    // pressure
    float pressure = max(-0.0f, stiffness * (pow(density/rest_density,4.) -1));
    mat3 stress = mat3(-pressure);
    mat3 strain = Cp + transpose(Cp);
    stress += dynamic_viscosity * strain;

    mat3 eq_16_term0 = -volume * 4 * stress * deltaTime;

    // loop neighbor nodes
    for (int i = 0; i < 3; ++i)
    for (int j = 0; j < 3; ++j)
    for (int k = 0; k < 3; ++k) {
        ivec3 node = base + ivec3(i, j, k);
        if (any(lessThan(node, ivec3(0))) || any(greaterThanEqual(node, ivec3(gridRes))))
            continue;

        int index = node.x + node.y * gridRes + node.z * gridRes * gridRes;
        vec3 dpos = (vec3(i,j,k) - fx) * dx;

        float weight = w[i].x * w[j].y * w[k].z;
        vec3 force = eq_16_term0 * weight * dpos;

        // atomic add to grid
        atomicAdd(gridStatus[index].x, force.x);
        atomicAdd(gridStatus[index].y, force.y);
        atomicAdd(gridStatus[index].z, force.z);
    }
}